# Contributor: john_schaf

pkgname=pastix
pkgver=5.2.2.22
pkgrel=1
_version_id=34974
pkgdesc="high performance parallel solver for very large sparse linear systems based on direct methods."
arch=('x86_64')
url="http://pastix.gforge.inria.fr/"
license=('CeCILL-C')
depends=('scotch' 'hwloc' 'blas')
replaces=()
conflicts=()
provides=()
options=('staticlibs')
source=(https://gforge.inria.fr/frs/download.php/${_version_id}/${pkgname}_${pkgver}.tar.bz2 config.in)
md5sums=('8733696b3cb91a7f0aa5b0d050a9f115' 'd96d2211708eb3a23d0b9598b39ac566')

_extractdir=${pkgname}_${pkgver}

prepare() {
  cd "${srcdir}/${_extractdir}/src"

  cp "${srcdir}/config.in" .
  echo "cat ./Revision" > myversion.sh
}

build() {
  cd "${srcdir}/${_extractdir}/src"

  make all
  make install
}

check() {
  cd "${srcdir}/${_extractdir}/src"

  make examples
  ./example/bin/simple -lap 100 
  #valgrind --leak-check=yes ./example/bin/simple -lap 100 
}

package() {
  cd "${srcdir}/${_extractdir}/install"

  sed 's#="-.*install#="#' pastix-conf > bin/pastix-conf
  sed -i 's#LIBS="#LIBS="-L/usr/lib#' bin/pastix-conf
  sed -i 's#INC="#INC="-I/usr/include#' bin/pastix-conf
  
  install -D -m 644 include/pastix.h "$pkgdir/usr/include/pastix.h"
  install -D -m 644 include/* "$pkgdir/usr/include/"
  install -D -m 644 lib/libpastix.a "$pkgdir/usr/lib/libpastix.a"
  install -D -m 644 lib/* "$pkgdir/usr/lib/"
  install -D -m 755 bin/pastix-conf "$pkgdir/usr/bin/pastix-conf"
}

# vim:set ts=2 sw=2 et:
